#+title: ob-llm
* ob-llm
:PROPERTIES:
:header-args:emacs-lisp: :tangle ob-llm.el
:END:

** Preamble, and requirements.
#+begin_src emacs-lisp
;;; ob-llm.el --- org-babel integration for llm providers
;; copyright (c) 2024 Jiyan Joans Schneider
;; author: Jiyan Jonas Schneider <mail@jiyanjs.com>
;; keywords: tools, ai, babel
;; version: 0.1.0
;; package-requires: ((emacs "28.1") (org "9.6"))

;;; commentary:
;; lightweight llm integration for org-babel

;;; code:
(require 'ob)
(require 'llm)
(require 'org-element)
#+end_src

#+RESULTS:
: org-element

** defvars and defgroups
Here we define the group and some variables.
I think this is mostly done for customization.
#+begin_src emacs-lisp
(defgroup ob-llm nil
  "llm babel integration"
  :group 'external)

(defvar org-babel-default-header-args:llm
  '((:async . "yes")
    (:results . "replace raw")
    (:session . "none")
    (:context . "file"))
  "Default arguments for llm source blocks.")

(defvar ob-llm-providers nil
  "Mapping of provider names to llm provider objects.
Users should populate this with their specific providers.")

(defvar ob-llm-system-prompt
  "META_PROMPT1: Follow the prompt instructions laid out below. You are an assistant living in the users' emacs. Be helpful to them. Your output will be seen their emacs.\n\n User File: "
  "System prompt for the language model.")

(defcustom ob-llm-default-provider nil
  "Default provider for llm babel blocks.
Must be set by the user to one of the registered providers in `ob-llm-providers'."
  :type '(choice (const :tag "Not Set" nil)
          (string :tag "Provider Name"))
  :group 'ob-llm)
#+end_src

#+RESULTS:
: ob-llm-default-provider

** Dealing with the Provider

We want to use ~llm.el~ as a backend and so we will just use llm providers.

#+begin_src emacs-lisp
(defun ob-llm-extract-provider (provider providers)
  (alist-get provider providers nil nil #'string=))

(defun ob-llm-get-provider (params)
  "Get the llm provider from PARAMS or default.
Raises an error with helpful instructions if no provider found."
  (let* ((provider-param (cdr (assq :provider params)))
         (provider (cond
                    ;; If a provider is specified in params, look it up by name
                    (provider-param
                     (ob-llm-extract-provider provider-param ob-llm-providers))
                    ;; If default provider is already a provider object, use it directly
                    ((and ob-llm-default-provider
                          (not (stringp ob-llm-default-provider))) ob-llm-default-provider)
                    ;; If default provider is a string, look it up
                    (ob-llm-default-provider
                     (cdr (assoc ob-llm-default-provider ob-llm-providers))))))
    (unless provider
      (error "No LLM provider found.
Please set `ob-llm-default-provider' or provide a :provider parameter.
Available providers: %s"
             (mapcar #'car ob-llm-providers))) provider))
#+end_src

#+RESULTS:
: ob-llm-get-provider

Usage, input and output are like this:
#+begin_src emacs-lisp :tangle tests/ob-llm-test.el
(ert-deftest ob-llm-test-extract-provider ()
  (let ((qwen-provider (ob-llm-extract-provider 'qwen ob-llm-providers)))
    (should qwen-provider)
    (should (eq (type-of qwen-provider) 'llm-ollama))
    (should (string-match-p "qwen" (format "%S" qwen-provider)))))
#+end_src

#+RESULTS:
: ob-llm-test-extract-provider

Next, I want to write a test for the get-provider function

#+begin_src emacs-lisp :tangle tests/ob-llm-test.el
(ert-deftest ob-llm-test-get-provider ()
    ;; Test getting provider by default
    (should (ob-llm-get-provider '(haiku))))
#+end_src

#+RESULTS:
: ob-llm-test-get-provider

** Executing the llm

#+begin_src emacs-lisp
(defun org-babel-execute:llm (body params)
  "Execute a block of llm code with org-babel."
  (let* ((element (org-element-at-point))
         (block-start (org-element-property :begin element))
         (context-type (or (cdr (assq :context params)) "file"))
         (context (pcase context-type
                    ("heading" (ob-llm-get-heading-context element))
                    (_ (buffer-substring-no-properties (point-min) block-start)))))
    (if (assq :async params)
        (let ((context context)) ; Store context in let binding
          (with-current-buffer (current-buffer)
            (setq-local llm-stored-params params)
            ;; (message "context %s" context)
            (org-babel-insert-result "Loading..." llm-stored-params))
          (llm-chat-async
           (ob-llm-get-provider params)
           (llm-make-chat-prompt
            (format "META_PROMPT: %s\nContext:\n%s\n\nPrompt:\n%s"
                    ob-llm-system-prompt context body))
           (lambda (response)
             (with-current-buffer (current-buffer)
               (org-babel-insert-result response llm-stored-params)))
           (lambda (err msg)
             (with-current-buffer (current-buffer)
               (org-babel-insert-result
                (format "Error: %s - %s" err msg) llm-stored-params))))
          nil)
      (llm-chat
       (ob-llm-get-provider params)
       (llm-make-chat-prompt (format "Context:\n%s\n\nPrompt:\n%s" context body))))))
#+end_src

#+RESULTS:
: org-babel-execute:llm

#+begin_src emacs-lisp :tangle tests/ob-llm-test.el
(ert-deftest ob-llm-test-extract-provider ()
  (let ((params '((:async . nil) (:provider . "qwen")))
        (body "What is 2+2?"))
    (should (stringp (org-babel-execute:llm body params)))))
#+end_src

#+RESULTS:
: ob-llm-test-extract-provider

** Functions for playing with the context Context

#+begin_src emacs-lisp
(defun ob-llm-get-heading-context (element)
  "Get context from current top-level heading to ELEMENT's begin."
  (save-excursion
    (goto-char (org-element-property :begin element))
    (let* ((heading (org-element-lineage element '(headline)))
           (top-heading (when heading
                          (while (and heading (> (org-element-property :level heading) 1))
                            (setq heading (org-element-property :parent heading)))
                          heading)))
      (if top-heading
          (buffer-substring-no-properties
           (org-element-property :begin top-heading)
           (org-element-property :end element))
        ""))))
#+end_src

#+RESULTS:
: ob-llm-get-heading-context

#+begin_src emacs-lisp :tangle no
(with-temp-buffer
  (org-mode)
  (insert "*FIRST HEADLINE \n* Top\nsome content\n** Sub\nmore stuff\n*** Here\nfinal text\n* Another headline \n HERE")
  (goto-char 50)
  (ob-llm-get-heading-context (org-element-at-point)))
#+end_src

#+RESULTS:
#+begin_example
,* Top
some content
,** Sub
more stuff
#+end_example


#+begin_src emacs-lisp :tangle no
(with-temp-buffer
  (org-mode)
  (insert "*FIRST HEADLINE \n* Top\nsome content\n** Sub\nmore stuff\n*** Here\nfinal text\n* Another headline \n HERE")
  (goto-char (point-max))
  (ob-llm-get-heading-context (org-element-at-point)))

#+end_src

#+RESULTS:
: * Another headline
:  HERE

#+begin_src emacs-lisp :tangle tests/ob-llm-test.el
(ert-deftest ob-llm-test-heading-context ()
  (with-temp-buffer
    (org-mode)
    (insert "*FIRST HEADLINE \n* Top\nsome content\n** Sub\nmore stuff\n*** Here\nfinal text\n* Another headline \n HERE")
    (goto-char 50)
    (should (string= (ob-llm-get-heading-context (org-element-at-point))
                     "* Top\nsome content\n** Sub\nmore stuff\n"))
    (goto-char (point-max))
    (should (string= (ob-llm-get-heading-context (org-element-at-point))
                     "* Another headline\n HERE"))))
#+end_src

#+RESULTS:
: ob-llm-test-heading-context

** Postamble

#+begin_src emacs-lisp
;; register the language
(add-to-list 'org-babel-load-languages '(llm . t))
(org-babel-do-load-languages 'org-babel-load-languages org-babel-load-languages)

(provide 'ob-llm)
;;; ob-llm.el ends here
#+end_src

#+RESULTS:
: ob-llm

* Other useful stuff for myself
Change the "current buffer for execution of some functions

#+begin_src emacs-lisp :tangle no
(with-temp-buffer
    (insert "*FIRST HEADLINE \n* Top\nsome content\n** Sub\nmore stuff\n*** Here\nfinal text\n* Another headline \n HERE")
    (cadr (org-element-parse-buffer))
#+end_src

#+RESULTS:
| :standard-properties | [1 1 1 100 100 0 nil org-data nil nil nil 3 100 nil #<killed buffer> nil nil nil] | :path | nil | :CATEGORY | nil |
